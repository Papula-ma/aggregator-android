apply plugin: 'com.android.application'

android {
    compileSdkVersion 20
    buildToolsVersion '20.0.0'

    defaultConfig {
        applicationId 'com.tughi.aggregator2'
        minSdkVersion 16
        targetSdkVersion 20
        versionCode 100
        versionName '3.0'
    }
}

dependencies {
    compile 'com.android.support:support-v4:20.0.0'
    compile project(':sqlite')
    compile project(':core')
}

task generateDrawables << {
    def svgDir = new File(projectDir, ['src', 'main', 'svg'].join(File.separator))
    def resDir = new File(projectDir, ['src', 'main', 'res'].join(File.separator))

    def DENSITY_RATIOS = ['mdpi': 1, 'hdpi': 1.5, 'xhdpi': 2, 'xxhdpi': 3, 'xxxhdpi': 4]

    def generateDrawable = { name, width, height, densities ->
        def svgFile = new File(svgDir, name + '.svg')

        densities.each() { density ->
            def drawableFile = new File(resDir, 'drawable-' + density + File.separator + name + '.png')

            if (!drawableFile.exists() || drawableFile.lastModified() < svgFile.lastModified()) {
                def w = (int) width * DENSITY_RATIOS[density]
                def h = (int) height * DENSITY_RATIOS[density]
                def command = "inkscape -e $drawableFile -w$w -h$h $svgFile"

                def process = command.execute()
                process.waitFor()
            }
        }
    }

    generateDrawable('ic_launcher', 48, 48, ['mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi'])

}
