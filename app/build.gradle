apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId 'com.tughi.aggregator.next'
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 300000
        versionName '3.0.0'
    }
}

dependencies {
    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0-alpha1'

    compile 'android.arch.persistence.room:runtime:1.0.0-alpha1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.tughi.android:sqlite-open-helper:0.5'
    compile 'com.tughi.sax:sax-framework:2.0'
}

task generateDrawables << {
    def inkscapeCmd = 'inkscape'

    def svgDir = new File(projectDir, ['src', 'main', 'svg'].join(File.separator))
    def resDir = new File(projectDir, ['src', 'main', 'res'].join(File.separator))

    def DENSITIES = [
            '-mdpi'   : 90,
            '-hdpi'   : 135,
            '-xhdpi'  : 180,
            '-xxhdpi' : 270,
            '-xxxhdpi': 360
    ]

    def generate = { String drawable ->
        new File(svgDir, drawable).eachFileMatch(~/.*\.svg/) { svg ->
            DENSITIES.each { type, dpi ->
                def png = new File(new File(resDir, drawable + type), svg.name.replace('.svg', '.png'))
                if (!png.exists() || png.lastModified() < svg.lastModified()) {
                    "$inkscapeCmd -e $png -d$dpi $svg"
                            .execute()
                            .waitFor()
                }
            }
        }
    }

    generate('drawable')
    generate('mipmap')
}

preBuild.dependsOn generateDrawables
